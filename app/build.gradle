apply plugin: 'com.android.application'
apply plugin: 'realm-android'

def getProperty(String filename, String propName) {
    def propsFile = rootProject.file(filename)
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        if (props[propName] != null) {
            return props[propName]
        } else {
            print("No such property " + propName + " in file " + filename)
        }
    } else {
        print(filename + " does not exist!")
    }
}

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 25
        versionName "1.1.6"
        vectorDrawables.useSupportLibrary = true
        buildConfigField "String", "VERSION_DATE", '"25 Jul 2018"'
        project.ext.set("archivesBaseName", "youshows-v" + versionName)
        buildConfigField "String", "TMDB_API_KEY", "\"${getProperty("local.properties", "tmdb_api_key")}\""
        buildConfigField "String", "TRAKT_CLIENT_ID", "\"${getProperty("local.properties", "tract_client_id")}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "market", "develop"

    productFlavors {
        releasing {
            minSdkVersion 21
            flavorDimensions "market"
            applicationId "org.michaelbel.shows"
        }

        developing {
            minSdkVersion 21
            flavorDimensions "develop"
            versionName "1.1.6-beta"
            applicationId "org.michaelbel.shows.beta"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    realm {
        syncEnabled = true
    }
}

dependencies {
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'org.michaelbel:material:0.3.6'
    implementation 'org.michaelbel:bottomsheet:1.2.2'
    implementation 'io.realm:android-adapters:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.7'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'at.blogc:expandabletextview:1.0.5'
    //implementation 'com.google.android.material:material:1.0.0'
}

repositories {
    mavenCentral()
}